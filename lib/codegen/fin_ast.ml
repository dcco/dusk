open Parser.Dusk_ast
open Fin_type

	(*
		representation for raw code
	*)

type gen_exp =
	ConstExpC of const
	| NullExpC
	(*| LitExpC of int *)
	| VarExpC of string
	| UnaryExpC of string * gen_exp
	| BinExpC of string * gen_exp * gen_exp
	| CallExpC of gen_exp * gen_exp list * g_type
		(* boxes a value inside a pointer 
	| BoxExpC of int * gen_exp * g_type*)
		(* tuple operations (tuple creation also requires a box) *)
	| TupleExpC of int * g_type * gen_exp list
	| TagTupleExpC of int * g_type * string * gen_exp list
	| TupleIndexExpC of gen_exp * int * g_type
		(* array operations *)
	| ConstArrayExpC of int list * gen_exp list * g_type
	| NewArrayExpC of gen_exp list * gen_exp list * g_type
	| ArrayIndexExpC of gen_rw * gen_exp * gen_index * g_type
	| ArrayLengthExpC of gen_exp
	| ArrayDimsExpC of int * gen_exp
		(* struct operations *)
	| NewStructExpC of string * gen_exp list
	| StructFieldExpC of gen_rw * gen_exp * int * string
		(* garbage collection *)
	| GCNewRootExpC of gen_exp
and gen_rw = RC | WC of gen_exp
and gen_index =
	RawIndexC of gen_exp
	| FullIndexC of gen_exp list
and gen_stmt =
	EvalStmtC of gen_exp
	| AssignStmtC of string * gen_exp
	| ReturnStmtC of gen_exp option
	| VarStmtC of string * gen_exp * g_type
	| IfStmtC of gen_exp * gen_stmt list * bool * gen_stmt list * bool
	| WhileStmtC of gen_exp * gen_stmt list
		(* garbage collection *)
	| GCCollectStmtC

type gen_met =
	MethodC of (string * g_type) list * g_type * gen_stmt list

let type_of_method (MethodC(arg_l, tau_r, _): gen_met): g_type list * g_type =
	(List.map (fun (_, tau) -> tau) arg_l, tau_r)

type gen_dec =
	FunDecC of gen_met
	| TDefDecC of g_tdef
	| GlobalDecC of bool * gen_exp

	(*
		auxiliary gathering functions
	*)
let rec collect_thru_exp (f: gen_exp -> 'a list) (e: gen_exp): 'a list = match e with
	UnaryExpC(_, e) -> collect_thru_exp f e
	| BinExpC(_, e1, e2) -> (collect_thru_exp f e1) @ (collect_thru_exp f e2)
	| CallExpC(ef, el, _) -> (collect_thru_exp f ef) @ (List.concat (List.map (collect_thru_exp f) el))
	| TupleExpC(_, _, el) -> (f e) @ (List.concat (List.map (collect_thru_exp f) el))
	| TagTupleExpC(_, _, _, el) -> (f e) @ (List.concat (List.map (collect_thru_exp f) el))
	| TupleIndexExpC(e, _, _) -> collect_thru_exp f e
	| NewArrayExpC(dim_l, el, _) ->
		(List.concat (List.map (collect_thru_exp f) dim_l)) @
		(List.concat (List.map (collect_thru_exp f) el))
	| ArrayIndexExpC(_, e, RawIndexC ei, _) -> (collect_thru_exp f e) @ (collect_thru_exp f ei)
	| ArrayIndexExpC(_, e, FullIndexC el, _) -> (collect_thru_exp f e) @ (List.concat (List.map (collect_thru_exp f) el))
	| NewStructExpC(_, el) -> List.concat (List.map (collect_thru_exp f) el)
	| StructFieldExpC(_, e, _, _) -> collect_thru_exp f e
	| _ -> f e
and collect_thru_stmt (f: gen_exp -> 'a list) (s: gen_stmt): 'a list = match s with
	EvalStmtC e -> collect_thru_exp f e
	| AssignStmtC(_, e) -> collect_thru_exp f e
	| ReturnStmtC eo -> (match eo with None -> [] | Some e -> collect_thru_exp f e)
	| VarStmtC(_, e, _) -> collect_thru_exp f e
	| IfStmtC(e, b1, _, b2, _) -> (collect_thru_exp f e) @ (collect_thru_body f b1) @ (collect_thru_body f b2)
	| WhileStmtC(e, b) -> (collect_thru_exp f e) @ (collect_thru_body f b)
	| GCCollectStmtC -> []
and collect_thru_body (f: gen_exp -> 'a list) (b: gen_stmt list): (int * g_type) list = match b with
	[] -> []
	| s :: st -> (collect_thru_stmt f s) @ (collect_thru_body f st)

let collect_box_body = collect_thru_body (fun e -> match e with
	TupleExpC(i, tau, _) -> [(i, tau)]
	| TagTupleExpC(i, tau, _, _) -> [(i, tau)]
	| _ -> []
)

let rec collect_var_stmt (s: gen_stmt): (string * g_type) list = match s with
	EvalStmtC _ -> []
	| AssignStmtC(_, _) -> []
	| ReturnStmtC _ -> []
	| VarStmtC(x, _, tau) -> [(x, tau)]
	| IfStmtC(_, b1, _, b2, _) -> (collect_var_body b1) @ (collect_var_body b2)
	| WhileStmtC(_, b) -> collect_var_body b
	| GCCollectStmtC -> []
and collect_var_body (b: gen_stmt list): (string * g_type) list = match b with
	[] -> []
	| s :: st -> (collect_var_stmt s) @ (collect_var_body st)